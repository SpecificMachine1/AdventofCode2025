(library (day day01)
  (export get-data count-zeros count-zero-passes)
  (import (rnrs base)
          (aoc file)
          (rnrs io simple))
(define (get-data filename)
  (let ((char-lines (filename->char-lines filename)))
    (define (char-line->rotation char-line)
      (list (list->string (list (car char-line)))
            (string->number (list->string (cdr char-line)))))
    (map char-line->rotation char-lines)))

(define (count-zeros rotations)
  (let dial-loop ((rot rotations) (position 50) (zeros 0))
    (cond
      ((null? rot) zeros)
      ((string=? "L" (caar rot))
       (let* ((this-turn (cadar rot))
              (new-position (mod (- position this-turn) 100)))
         (dial-loop (cdr rot)
                    new-position
                    (if (zero? new-position) (+ zeros 1) zeros))))
      ((string=? "R" (caar rot))
       (let* ((this-turn (cadar rot))
              (new-position (mod (+ position this-turn) 100)))
         (dial-loop (cdr rot)
                    new-position
                    (if (zero? new-position) (+ zeros 1) zeros))))
      (else (error "unrecognized letter" (caar rot))))))

(define (count-zero-passes rotations)
  (let dial-loop ((rot rotations) (position 50) (passes 0) (zeros 0) (double-counted 0))
    (if (null? rot)
      (- (+ passes zeros) double-counted)
      (let* ((dir (caar rot))
             (this-turn (* (cadar rot)
                           (cond ((string=? dir "L") -1)
                                 ((string=? dir "R") 1)
                                 (else (error "unrecognized letter" dir)))))
             (pos+this-turn (+ position this-turn))
             (new-pos (mod pos+this-turn 100)))
        (dial-loop (cdr rot)
                   new-pos
                   (+ passes (abs (div pos+this-turn 100)))
                   (if (zero? new-pos) (+ zeros 1) zeros)
                   (if (or (and (zero? position) (string=? "L" dir))
                           (and (zero? new-pos) (> (div pos+this-turn 100) 0)))
                     (+ 1 double-counted)
                     double-counted))))))
  )
